```
module ASYN_FIFO #(
    parameter   DATA_WIDRH  =   16,
    parameter   DATA_DEPTH  =   1024,
    parameter   ADDR_WIDTH  =   10
)
(
    input                           wclk,
    input                           wrst,
    input                           wen,
    input   [DATA_WIDRH - 1 : 0]    wData,
    input                           rclk,
    input                           rrst,
    input                           ren,
    output  [DATA_WIDRH - 1 : 0]    rData,
    output  reg                     full,
    output  reg                     empty
);
reg     [DATA_WIDRH - 1 : 0]    mem     [0 : DATA_DEPTH - 1];
wire    [ADDR_WIDTH - 1 : 0]    raddr;
wire    [ADDR_WIDTH - 1 : 0]    waddr;
reg     [ADDR_WIDTH     : 0]    rptr, rptr_w1, rptr_w2;                 //编码地址比RAM地址多一位,目的是方便使用格雷码判断空满信号
reg     [ADDR_WIDTH     : 0]    wptr, wptr_r1, wptr_r2;
reg     [ADDR_WIDTH     : 0]    wbin, rbin;
wire    [ADDR_WIDTH     : 0]    wbinnext, rbinnext, wgraycode, rgraycode;
wire                            empty_val, full_val;
//RAM
assign raddr = rbin[ADDR_WIDTH - 1 : 0];
assign waddr = wbin[ADDR_WIDTH - 1 : 0];
assign rData = mem[raddr];
always @(posedge wclk) begin
    if (wen & (!full))
        mem[waddr] <= wData;
end
//address generate
assign wbinnext = wbin + (wen & (!full));
assign rbinnext = rbin + (ren & (!empty));
assign wgraycode = (wbinnext >> 1) ^ wbinnext;
assign rgraycode = (rbinnext >> 1) ^ rbinnext;

always @(posedge wclk) begin
    if (wrst) begin
        {wbin, wptr} <= {2*ADDR_WIDTH{1'b0}};
    end
    else begin
        {wbin, wptr} <= {wbinnext, wgraycode};
    end
end
always @(posedge rclk) begin
    if (rrst) begin
        {rbin, rptr} <= {2*(ADDR_WIDTH+1){1'b0}};
    end
    else begin
        {rbin, rptr} <= {rbinnext, rgraycode};
    end
end
always @(posedge wclk) begin
    if (wrst) {rptr_w2, rptr_w1} <= {2*(ADDR_WIDTH+1){1'b0}};
    else {rptr_w2, rptr_w1} <= {rptr_w1, rptr};
end
always @(posedge rclk) begin
    if (rrst) {wptr_r2, wptr_r1} <= {2*ADDR_WIDTH{1'b0}};
    else {wptr_r2, wptr_r1} <= {wptr_r1, wptr};
end
//generate empty and full
assign empty_val = (rgraycode == wptr_r2);
assign full_val = (wgraycode == {{~rptr_w2[ADDR_WIDTH : ADDR_WIDTH - 1]}, rptr_w2[ADDR_WIDTH - 2 : 0]});
always @(posedge wclk) begin
    if (wrst) full <= 1'b0;
    else full <= full_val;
end
always @(posedge rclk) begin
    if (rrst) empty <= 1'b1;
    else empty <= empty_val;
end
endmodule

module ASYN_FIFO_tb (

);
parameter   CLK_PERIOD  =   10;
parameter   RESET_HOLD  =   30;
parameter   DATA_WIDTH  =   16;
parameter   DATA_DEPTH  =   1024;
parameter   ADDR_WIDTH  =   10;

reg                             wclk;
reg                             rclk;
reg                             wrst;
wire                            wen;
reg                             rrst;
reg                             ren;
reg     [DATA_WIDTH - 1 : 0]    iData   =   0;
wire    [DATA_WIDTH - 1 : 0]    oData;
wire                            full;
wire                            empty;

initial begin
    rrst = 1;
    wrst = 1;
    # RESET_HOLD;
    rrst = 0;
    wrst = 0;
end

always begin
    wclk = 0;
    #(CLK_PERIOD/2);
    wclk = 1;
    #(CLK_PERIOD/2);
end

always begin
    rclk = 0;
    #(CLK_PERIOD);
    rclk = 1;
    #(CLK_PERIOD);
end

always @(posedge wclk) begin
    if (wrst) begin
        ren <= 0;
    end
    else begin
        ren <= ($random % 2);
    end
end

always @(posedge wclk) begin
    if (wrst) begin
        iData <= 16'd0;
    end
    else if (full) begin
        iData <= iData;
    end
    else begin
        iData <= iData + 1'b1;
    end
end
assign wen = (!full);

ASYN_FIFO #(
    .DATA_WIDRH(16  ),
    .DATA_DEPTH(1024),
    .ADDR_WIDTH(10  )
)
FIFO(
    .wclk   (wclk   ),
    .wrst   (wrst   ),
    .wen    (wen    ),
    .wData  (iData  ),
    .rclk   (rclk   ),
    .rrst   (rrst   ),
    .ren    (ren    ),
    .rData  (oData  ),
    .full   (full   ),
    .empty  (empty  )
);

endmodule



```
