1. 复位值不是常量而是寄存器的输出，虽然综合可以通过将Q和rst_n进行逻辑组合之后输入到寄存器的 复位和置位端来实现这种逻辑，但是在异步复位的时候还是容易出问题，应该避免。
2. SPI master 的时钟关系  `sclk <= clk_sys / 2`
3. SPI slave 的时钟关系，clk_sys >= 10*sclk，因为涉及对SCLK的采样以及取沿，导致实际产生的tx_edge和rx_edge会相对于master产生的SCLK有delay，tx_edge驱动的数据被master的rx_edge采样需要满足足够的setup time
4. I2C master的时钟关系，I2C需要按照spec UM10204 中描述的相关timing关系进行i2c时序的建立，设计使用 5*SCL 进行start stop等信号的产生，因此需要prescale PCLK到5*SCL，另外为了对输入的SCL和SDA进行filter（i2c使用OD门线与逻辑设计，信号质量较差，另外SDA和SCL是由多个设备共享，且可以同时控制，很容易引入毛刺），设计使用大于20*SCL的时钟作为filter时钟。但是因为做了fliter，对于SCL和SDA总线上面的数据采样会有dleay，会造成SCL的高低电平时间拉长（例如SCL拉高后需要一段时间才能检测到，造成SCL的高电平时间相对于设计的会长一点）（XILINX PG090的IP Core 里面对于时钟要求是不小于25M且大于SCL的25倍）
5. 调用DW的prod_sum_pipe或者mult_pipe时候，由于designware不支持反压，只有一个en信号，可以通过将en信号与内部几拍的 valid信号或起来同时与上最后一级的ready，来实现反压暂停掉该DW，但是这样做只能做到反压功能，无法提高系统的performance，因为这样做不同于pipe_slice，pipe_slice在o_vld 为0时会向前级给ready要数据，从而充分利用内部每一级reg，但是我们这种修改无法通过内部的几级reg吃掉后级的反压。标准的做法应该是在DW后面再增加一个同深度的fifo，来将这部分的反压吃掉，但是这样会为了performance带来面积的增加。
6. 使用verilog mode 最好不要有多为数组在接口上，使用verilog mode TEMPLATE最好带上[]，不然信号autowire没有位宽
7. spyglass 对于补零的位宽会忽略，例如 `c  = $signed(a)x$signed({1'b0,b})`，a为10bit，b为10bit，c自动推断为20bit，不会因为补0变成21bit
8. 4p_to_8p, 8p_tp_4p 之间的pipe虽然只有4拍，但是因为4p转8p后valid不连续，如果ready反压的话4拍的pipe最多能存4拍8p数据，但是这个在前面的4p阶段等同于8拍4p数据，所以ready实际上ready会更晚才会反压到前级的4p，在需要pipeline对齐的时候需要考虑fifo深度，例如G通道通过fifo delay的时候就出现了问题
9. 8p_to_4p还有一个问题，后级4p反压的话，需要考虑前级能不能更新，如果是ready直接捅过去的话，会造成数据丢失，因为前面到一拍更新8个像素，后级只能取走4个像素，所以前级数据更新的条件应该是要看当前级有没有数据，并且还要考虑bubble问题，在没有ready的时候valid能够正常打到最后一级，可以分成三种情况(1)当前级有数据且是输出4p的最后一拍，则可以更新数据，(2)如果当前级没有数据也可以更新进来（通过一个buffer_empty的flag来识别），(3)如果有数据并且是8p里面第一个4p，则不能更新数据
下面是当时写的代码，当时调了挺久，遇到的主要问题有两个
(1) 性能问题，如何在当前buffer有空间的时候就开始找前级要数据，同时保证不会传给后级的时候丢失数据
(2) 非整倍数的padding问题，假如一行数据不是8的整倍数，但是数据又是连续给的时候（当前行最后不够8个，但是紧接着下个cycle下一行数据又进来），如何才能保证数据正确输出到后级而且不会错误的反压前级，并且要正确的产生eol信号，后面转8p的时候也得能够过滤掉多余的valid
```
   //- 4p_to_8p
    assign valid[0]      = (i_valid & wr_ptr_4p) | (i_valid & i_eol);
    assign o_ready       = (~wr_ptr_4p & ~i_eol) | ready[0];
    assign eol_r[0]      = i_eol & valid[0] & ready[0];
    assign pad_flag_r[0] = i_valid & o_ready & i_eol & ~wr_ptr_4p;
    always_ff @(posedge clk or negedge rst_n) begin : _8p_valid
        if (~rst_n) begin
            wr_ptr_4p <= 1'b0;
        end else if (start) begin
            wr_ptr_4p <= 1'b0;
        end else if (eol_r[0] & valid[0] & ready[0]) begin
            wr_ptr_4p <= 1'b0;
        end else if (i_valid & o_ready) begin
            wr_ptr_4p <= ~wr_ptr_4p;
        end
    end
    always_ff @(posedge clk or negedge rst_n) begin : _latch_input
        if (~rst_n) begin
            for (int i = 0; i < PPC; i = i + 1) begin
                hor_lf_in_r[i] <= (BW_IMG_WIDTH * CHNL_NUM)'(0);
            end
        end else if (i_valid & o_ready) begin
            for (int i = 0; i < PPC; i = i + 1) begin
                hor_lf_in_r[i] <= i_hor_lf[i];
            end
        end
    end
    always_comb begin : _4p_pack_8p
        for (int i = 0; i < PPC; i = i + 1) begin
            hor_lf[i+PPC] = i_hor_lf[i];
            hor_lf[i]     = pad_flag_r[0] ? i_hor_lf[i] : hor_lf_in_r[i];
        end
    end
```
```
always_ff @(posedge clk or negedge rst_n) begin : _8p_to_4p
        if (~rst_n) begin
            rd_ptr_4p <= 1'b0;
        end else if (start) begin
            rd_ptr_4p <= 1'b0;
        end else if (valid_4p_raw & ready_4p) begin
            rd_ptr_4p <= ~rd_ptr_4p;
        end
    end
    logic buffer_empty;
    always_ff @(posedge clk or negedge rst_n) begin : _buffer_empty
        if (~rst_n) buffer_empty <= 1'b1;
        else if (start) buffer_empty <= 1'b1;
        else if (pipe_en[STAGE_NUM0-1]) buffer_empty <= 1'b0;
        else if (valid_4p & ready_4p & rd_ptr_4p) buffer_empty <= 1'b1;
    end
    //1. data comes
    //2. last one byte in buffer
    //3. mask the pad cycle
    assign valid_4p_raw      = (valid[STAGE_NUM0] | rd_ptr_4p);
    assign valid_4p_mask     = pad_flag_r[STAGE_NUM0] & rd_ptr_4p;
    assign valid_4p          = valid_4p_raw & ~(valid_4p_mask);
    //1. no data in buffer
    //2. one 4p in buffer and i_ready come
    assign ready[STAGE_NUM0] = buffer_empty | (valid_4p_raw & ready_4p & rd_ptr_4p);
    always_comb begin : _eol_4p
        if (valid_4p & ready_4p) begin
            if (pad_flag_r[STAGE_NUM0]) eol_4p = eol_r[STAGE_NUM0];
            else eol_4p = eol_r[STAGE_NUM0] & rd_ptr_4p;
        end else begin
            eol_4p = 1'b0;
        end
    end
    assign pipe_en[STAGE_NUM0] = valid_4p & ready_4p;
```
8. 连续的反压导致dummy line的valid行间隙被破坏了，反压的架构自己在dummy line重建hblank已经没有意义，因为如果后级ready长时间反压，会导致下一行的valid堆到上一行结尾，hblank就没了，所以需要在hblank期间做事情的时候还是得在这段时间拉低ready和valid，前面调整timing都是白瞎，只有vo调整才有效果。如果当前拍运算需要损失性能，暂停数据传输的话，不光给到前级的ready要拉低，给到后级的valid也要拉低，不然后级会有无效数据
9. 取反加一时候，取反必须带上符号位去取反，比如左边 对8bit取绝对值，应该写成 `a_abs[7:0] = ~a[7:0] + 1'b1` 而不是 `a_abs[7:0] = ~a[6:0]+1`，这样右侧会先对a高位补0然后取反
10. packed array声明为signed似乎有点问题，不能这样用，还是用的时候加上signed吧，非array声明为signed之后用的时候就可以不加$signed
11. 使用宏来减少有规律但是非数组的循环配置，``` `define lut(a) lut_``a ```，双``` `` ```的作用类似于隔开 例如，如果需要传入两个参数，``` `define lut(a,b) lut_``a``_``b  ```的方式，但是需要注意这个不能写道for循环里面，不然传进去的还是i和j，而不是对应的值
12. 写代码千万不能偷懒，一个always块里面同时对两拍打拍造成异常，`pipe_en[0] d1 <=  d0, else if pipe_en[1] d2<= d1`，这是个有优先级的if else，有时候容易大意

